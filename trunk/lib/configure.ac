# Process this file with autoconf to produce a configure script.
#=============================================================================
#
# Copyright (C) 1998-2010 The Boeing Company
#                         Ed Trettevik <eat@nodebrain.org>
#
# NodeBrain is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
#=============================================================================
# Program:  NodeBrain
#
# File:     configure.ac
#
# Title:    NodeBrain autoconf input file
#
# Function:
#
#   This file is processed by autoconf to create the configure script when
#   a NodeBrain developer is creating a distribution.  
#
# Description:
#
#   You will not need this file unless you are attempting to use autoconf
#   to build a new configure script to resolve problems on your platform.
#   If so, you would do something like the following.
#
#     o  Modify this file (configure.ac) as necessary.
#     o  Execute "make distclean" if you have already run configure.  This
#        will clean up files created previously by configure and make that
#        are not part of the NodeBrain distribution.
#     o  Execute autoconf in this directory to create new configure script.
#     o  Execute ./configure to create a new Makefile and nbcfg.h file.
#
#   See http://www.gnu.org/software/autoconf for more information.
#   
#=============================================================================
# Change History:
#
# Date       Name/Change
# ---------- -----------------------------------------------------------------
# 2004/03/25 Ed Trettevik (version 0.6.0)
#            This file was originally generated by autoscan from the source
#            code and then modified.
# 2005/03/27 eat 0.6.2  Modified for new release
# 2005/06/05 eat 0.6.3  Modified for new release
# 2005/10/11 eat 0.6.4  New release
# 2006/04/20 eat 0.6.5  New release
# 2006/05/15 eat 0.6.6  New release
# 2007/05/04 eat 0.6.7  New release
# 2007/05/17 eat 0.6.7  Perl Compatible Regular Expression library used
# 2007/05/17 eat 0.6.8  New release
# 2008/01/20 eat 0.6.9  New release
# 2008/03/06 eat 0.7.0  New release
# 2008/09/30 eat 0.7.2  New release
# 2008/10/17 eat 0.7.3  Using libtool now
# 2009/02/20 eat 0.7.6  Port to other platforms (windows plus others)
# 2009/12/03 eat 0.7.7  New release
# 2010/02/04 eat 0.7.8  New release
# 2010/02/25 eat 0.7.9  New release 
# 2010/05/06 eat 0.8.0  New release - multiple master message log support
# 2010/05/11 eat 0.8.1  Fix to defects in message cabal feature 
# 2010/05/21 eat 0.8.2  Fix more defects in message cabal feature 
# 2010/09/06 eat 0.8.3  Baseline module 
# 2010/10/14 eat 0.8.4  Patches and pid file 
# 2010/11/07 eat 0.8.5  Source line continuation - "\" at end of line
#=============================================================================

AC_PREREQ(2.62)
AC_INIT([libnb],[0.8.5],[bugs@nodebrain.org])
AC_CONFIG_SRCDIR([nb.h])
AC_CONFIG_HEADER([nbcfg.h])
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE

# define module directory variables

AC_DEFINE_UNQUOTED(NB_API_VERSION,"0",[NodeBrain API version])
NB_MODULE_PATH=".nb:/usr/local/lib/nb/0.8:/usr/lib/nb/0.8:/usr/local/lib/nb:/usr/lib/nb"
NB_MODULE_PATH_SEPARATOR=":"
AC_DEFINE_UNQUOTED(NB_MODULE_PATH,"${NB_MODULE_PATH}",[Module path])
AC_DEFINE_UNQUOTED(NB_MODULE_PATH_SEPARATOR,'${NB_MODULE_PATH_SEPARATOR}',[Module path separator])

AC_CONFIG_SUBDIRS([module])

# following recommended by libtoolize
AC_CONFIG_MACRO_DIR([m4])

# LIBTOOL stuff
LT_PREREQ([2.2.4])
LT_INIT
AC_SUBST([LIBTOOL_DEPS])
LT_SYS_MODULE_EXT
LT_FUNC_DLSYM_USCORE

# define a release date variable
AC_DEFINE(NB_RELEASE_DATE,"2010/11/07",[Define release date])

# Checks for programs.
AC_PROG_CC

# Checks for libraries.
LIBS="-lncurses"
AC_CHECK_LIB([pcre],[pcre_compile],,[AC_MSG_ERROR(Required library -lpcre not found. You may want to download it from http://www.pcre.org or locate it and include directory in LD_LIBRARY_PATH to support this build.)])
AC_CHECK_LIB([readline],[readline],,[AC_MSG_ERROR(Required library -lreadline not found. You may want to download it from http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html or locate it and include directory in LD_LIBRARY_PATH to support this build.)])
AC_CHECK_LIB(history,add_history,,[AC_MSG_ERROR(Required library -lhistory not found. You may want to download it from http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html or locate it and include directory in LD_LIBRARY_PATH to support this build.)])
AC_CHECK_LIB(ssl,SSL_library_init,,[AC_MSG_ERROR(Required library -lssl not found. You may want to download it from http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html or locate it and include directory in LD_LIBRARY_PATH to support this build.)])

AC_SEARCH_LIBS(socket,socket)
AC_SEARCH_LIBS(inet_ntoa,nsl)

AC_SEARCH_LIBS(fmod,m,,[AC_MSG_ERROR(Required math library -lm not found. Locate it and include in LD_LIBRARY_PATH to support this build.)])
#AC_CHECK_LIB(dl, main)

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netdb.h netinet/in.h stdlib.h string.h sys/socket.h sys/time.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T

# Checks for library functions.
AC_FUNC_FORK
#AC_FUNC_MALLOC messes up on my AIX with no rpl_malloc
AC_FUNC_MEMCMP
AC_FUNC_MKTIME
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STRTOD
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([alarm gethostbyaddr gethostbyname inet_ntoa memchr memset regcomp select socket strchr strrchr strspn strstr])

# check for platform requirements
#AC_CANONICAL_HOST

# define a release date variable
AC_DEFINE_UNQUOTED(NB_COMPILE_PLATFORM,"$host",[Define compile platform])

CFLAGS="-Wall $CFLAGS"
#CFLAGS="-Wall -I/usr/local/include -I/usr/local/ssl/include"
#LDFLAGS="-L/usr/local/ssl/lib"
case "$host" in
  *-netbsd*)
    AC_DEFINE(NETBSD,,[Define if NetBSD])
    AC_DEFINE(ANYBSD,,[Define if any BSD])
    #CFLAGS="${CFLAGS} -rdynamic"
    ;;
  *-openbsd*)
    AC_DEFINE(OPENBSD,,[Define if OpenBSD])
    AC_DEFINE(ANYBSD,,[Define if any BSD])
    #CFLAGS="${CFLAGS} -rdynamic"
    #LDFLAGS="${LDFLAGS} -Wl,-E"
    ;;
  *-sgi-irix*)
    AC_DEFINE(IRIX,,[Define if IRIX])
    ;;
  *-solaris*)
    AC_DEFINE(SOLARIS,,[Define if SOLARIS])
    #LDFLAGS="${LDFLAGS} -ldl -lnsl -lsocket"
    ;;
  *-sunos*)
    AC_DEFINE(SUNOS,,[Define if SUNOS])
    ;;
  *-linux*)
    AC_DEFINE(LINUX,,[Define if LINUX])
    #CFLAGS="${CFLAGS} -rdynamic"
    LDFLAGS="${LDFLAGS} -ldl"
    ;;
  *-hpux*)
    AC_DEFINE(HPUX,,[Define if HPUX])
    #LDFLAGS="${LDFLAGS} -ldld -Wl,-E"
    ;;
  *-freebsd*)
    AC_DEFINE(FREEBSD,,[Define if FREEBSD])
    AC_DEFINE(ANYBSD,,[Define if any BSD])
    #CFLAGS="${CFLAGS} -rdynamic"
    ;;
  *-bsdi*)
    AC_DEFINE(BSDI,,[Define if BSDI])
    AC_DEFINE(ANYBSD,,[Define if any BSD])
    #CFLAGS="${CFLAGS} -rdynamic"
    ;;
  *-aix*)
    AC_DEFINE(AIX,,[Define if AIX])
    #CFLAGS="${CFLAGS} -qmaxmem=8192"
    #LDFLAGS="${LDFLAGS} -ldl"
    ;;
# <platform>-apple-darwin* or <platform>-apple-rhapsody*    
  *-apple*)
    AC_DEFINE(MACOS,,[Define if MACOS])
    AC_DEFINE(ANYBSD,,[Define if any BSD])
esac

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
